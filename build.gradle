plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '1.2.1'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'com.github.breadmoirai.github-release' version '2.2.12'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "+mc" + project.minecraft_version
group = project.maven_group

repositories {
	mavenLocal()
	jcenter()
	maven {
		name "Fabric"
		url "https://maven.fabricmc.net/"
	}
	maven {
		name "Siphalor's Maven"
		url "https://maven.siphalor.de/"
	}
	maven {
		url "https://maven.shedaniel.me"
	}
	maven {
		url "https://maven.terraformersmc.com/releases"
	}
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_build}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	def fabric_deps = [
	        'fabric-api-base': '0.1.2+b7f9825d0c',
            'fabric-events-interaction-v0': '0.4.0+ec432e190c',
			'fabric-events-lifecycle-v0': '0.2.0+2f23104b0c',
			'fabric-item-api-v1': '1.1.0+ec432e190c',
			'fabric-lifecycle-events-v1': '1.2.0+f79427860c',
			'fabric-key-binding-api-v1': '1.0.1+06ca07090c',
            'fabric-networking-api-v1': '1.0.1+c1455e800c',
			'fabric-tag-extensions-v0': '0.1.3+abd915800c',
			'fabric-tool-attribute-api-v1': '1.2.2+db9ee7ac0c',
			'fabric-resource-loader-v0': '0.1.10+06c939b30c'
	]

	for (entry in fabric_deps.entrySet()) {
		modImplementation "net.fabricmc.fabric-api:" + entry.getKey() + ":" + entry.getValue()
		include "net.fabricmc.fabric-api:" + entry.getKey() + ":" + entry.getValue()
	}

	modImplementation "net.fabricmc.fabric-api:fabric-item-groups-v0:0.2.0+0121bd900c"

	modImplementation("com.terraformersmc:modmenu:1.10.6") {
		exclude group: "net.fabricmc.fabric-api"
	}

	include(modApi("de.siphalor.tweed4:tweed4-base-$minecraft_major_version:1.3.1+"))
	include(modApi("de.siphalor.tweed4:tweed4-annotated-$minecraft_major_version:1.2.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-data-$minecraft_major_version:1.2.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-data-hjson-$minecraft_major_version:1.1.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-tailor-screen-$minecraft_major_version:1.0.1+"))
	include(modApi("de.siphalor.tweed4:tweed4-tailor-coat-$minecraft_major_version:1.0.1+"))
	include(modApi("de.siphalor:coat-$minecraft_major_version:$coat_version"))

	modImplementation("de.siphalor:amecsapi-1.15:${project.amecs_version}") {
		exclude group: "net.fabricmc.fabric-api"
	}
	include "de.siphalor:amecsapi-1.15:${project.amecs_version}"

	// Smooth startup
	modRuntimeOnly("com.github.astei:lazydfu:master-SNAPSHOT")
}

//sourceSets.main.java.exclude("**/ModMenu*")

license {
	header = project.file('LICENSE_HEADER')

	include '**/*.java'
}

processResources {
    inputs.property "version", version

	afterEvaluate {
		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand "version": version
			duplicatesStrategy DuplicatesStrategy.INCLUDE
		}
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${project.archives_base_name}-${project.minecraft_major_version}"

			from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        if (project.hasProperty("siphalorMavenUser")) {
            maven {
                name = "Siphalor"
                url = "https://maven.siphalor.de/upload.php"
                credentials {
                    username = siphalorMavenUser
                    password = siphalorMavenPassword
                }
            }
        }
    }
}

// Mod sites

String getProjectChangelog() {
	def commits = 'git log -20 --format=oneline'.execute().text.readLines()
	def versionCommit = null
	for (commit in commits) {
		if (commit.contains('Version')) {
			versionCommit = commit.split(' ')[0]
			break
		}
	}
	if (versionCommit == null) {
		return """## Version ${project.mod_version}

No changelog found!"""
	}
	return "git log -1 --format=format:##%x20%s%n%n%b%nRelease%x20by%x20%an $versionCommit".execute().text.trim()
}

task uploadToModSites {
	dependsOn build
	group = "upload"
}

if (project.hasProperty("siphalorCurseForgeApi")) {
	curseforge {
		apiKey project.siphalorCurseForgeApi
		project {
			id = "317514"
			releaseType = project.mod_release
			changelogType = "markdown"
			changelog = getProjectChangelog()
			addGameVersion("Fabric")
			for (version in ((String) project.mod_mc_versions).split(";")) {
				addGameVersion(version)
			}
			relations {
				embeddedLibrary "fabric-api"
				embeddedLibrary "amecs"
				embeddedLibrary "tweed-api"
				optionalDependency "modmenu"
			}
			mainArtifact(remapJar) {
				displayName = "[${project.mod_mc_version_specifier}] ${project.mod_version}"
			}
		}
	}
	uploadToModSites.finalizedBy(tasks.curseforge)
}

import com.modrinth.minotaur.TaskModrinthUpload
if (project.hasProperty("siphalorModrinthApi")) {
	task modrinth (type: TaskModrinthUpload) {
		group = "upload"
		dependsOn(build)

		token = project.siphalorModrinthApi
		projectId = "u5Ic2U1u"
		versionNumber = version
		versionName = "[${project.mod_mc_version_specifier}] ${project.mod_version}"
		changelog = getProjectChangelog()
		uploadFile = remapJar
		versionType = project.mod_release
		for (version in ((String) project.mod_mc_versions).split(";")) {
			addGameVersion(version)
		}
		addLoader("fabric")
	}
	uploadToModSites.finalizedBy(tasks.modrinth)
}

if (project.hasProperty("siphalorGitHubToken")) {
	githubRelease {
		token siphalorGitHubToken
		targetCommitish.set(minecraft_major_version)
		releaseName.set("Version $mod_version for $mod_mc_version_specifier")
		body.set(getProjectChangelog())
		releaseAssets remapJar.getArchiveFile()
		prerelease.set(mod_release != "release")
		overwrite.set(true)
	}
	uploadToModSites.finalizedBy(tasks.githubRelease)
}
